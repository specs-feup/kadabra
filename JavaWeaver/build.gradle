plugins {
    id 'distribution'
    id 'java'
    id 'application'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()

    // Jadx
    google()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {
    testImplementation "junit:junit:4.13.1"

    implementation ':JadxPlus'
    implementation ':JavaGenerator'
    implementation ':jOptions'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'

    implementation ':KadabraAPI'

    implementation ':LanguageSpecification'
    implementation ':LaraUtils'
    implementation ':LARAI'
    implementation ':WeaverInterface'

    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.1'
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'org.ow2.asm:asm:9.8'

    weaverGeneratorRuntime ":WeaverGenerator"
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'api'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
    }
}

application {
    mainClass.set("weaver.gui.KadabraLauncher")
}

// Kadabra
tasks.register('kadabra', JavaExec) {
    group = "Execution"
    description = "Launches Kadabra"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    args = []
}

// Weaver Generator
tasks.register('generateWeaver', JavaExec) {
    group = "Execution"
    description = "Generates the join point classes from the Language Specification"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'

    args = [
        '-w', 'JavaWeaver',
        '-x', './resources/specs/kadabra',
        '-o', './src',
        '-p', 'weaver.kadabra',
        '-n', 'spoon.reflect.declaration.CtElement',
        "-e",
        '-j'
    ]
}

compileJava.dependsOn generateWeaver
