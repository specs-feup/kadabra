plugins {
    id 'distribution'
    id 'java' // Java project
    id 'application' // Executable
    id "org.sonarqube" version "6.0.1.5171"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

application {
    mainClass.set("weaver.gui.KadabraLauncher")
}


// Repositories providers
repositories {
    mavenCentral()

    // Gearman
    maven { url="https://oss.sonatype.org/content/repositories/snapshots" }

    // Jadx
    google()
}

dependencies {
    testImplementation "junit:junit:4.13.1"

    implementation ':JadxPlus'
    implementation ':JavaGenerator'
    implementation ':jOptions'
    implementation ':LogbackPlus'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'
    implementation ':JsEngine'

    implementation ':KadabraAPI'
    implementation ':KadabraLaraApi'

    implementation ':LanguageSpecification'
    implementation ':LaraCommonLanguageApi'
    implementation ':LaraDoc'
    implementation ':LaraFramework'
    implementation ':LaraUtils'
    implementation ':LARAI'
    implementation ':WeaverInterface'
    implementation ':JavaWeaverSpecs'

    implementation "org.apache.commons:commons-lang3:3.6"
    implementation "org.apache.commons:commons-exec:1.3"
    implementation "fr.inria.gforge.spoon:spoon-core:10.1.1"
    implementation "org.json:json:20190722"
    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'org.ow2.asm', name: 'asm', version: '7.1'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'api'
        }

        resources {
            srcDir 'api'
            srcDir 'resources'
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
        }

        resources {
            srcDir 'src-lara'
            srcDir 'resources'
        }
    }


}

sonar {
  properties {
    property "sonar.projectKey", "specs-feup_kadabra"
    property "sonar.organization", "specs-feup"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}


// Kadabra
task kadabra(type: JavaExec) {
    group = "Execution"
    description = "Launches Kadabra"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    args = []
}
