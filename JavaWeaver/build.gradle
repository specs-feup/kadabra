plugins {
    id 'distribution'
    id 'java'
    id 'application'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()

    // Gearman
    maven { url="https://oss.sonatype.org/content/repositories/snapshots" }

    // Jadx
    google()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {
    testImplementation "junit:junit:4.13.1"

    implementation ':JadxPlus'
    implementation ':JavaGenerator'
    implementation ':jOptions'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'
    implementation ':JsEngine'

    implementation ':KadabraAPI'

    implementation ':LanguageSpecification'
    implementation ':LaraUtils'
    implementation ':LARAI'
    implementation ':WeaverInterface'

    implementation group: 'org.apache.commons', name: 'commons-exec', version: '1.5.0'
    implementation group: 'fr.inria.gforge.spoon', name: 'spoon-core', version: '11.2.1'
    implementation group: 'org.json', name: 'json', version: '20250517'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.8'

    weaverGeneratorRuntime ":WeaverGenerator"
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'api'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'src-lara'
            srcDir 'resources'
        }
    }
}

application {
    mainClass.set("weaver.gui.KadabraLauncher")
}

// Kadabra
task kadabra(type: JavaExec) {
    group = "Execution"
    description = "Launches Kadabra"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    args = []
}

// Weaver Generator
task generateWeaver(type: JavaExec) {
    group = "Execution"
    description = "Generates the join point classes from the Language Specification"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'

    args = [
        '-w', 'JavaWeaver',
        '-x', './resources/specs/kadabra',
        '-o', './src',
        '-p', 'weaver.kadabra',
        '-n', 'spoon.reflect.declaration.CtElement',
        '-j',
        '-d'
    ]
}

compileJava.dependsOn generateWeaver
