plugins {
    id 'distribution'
    id 'java'
    id 'application'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()

    // Gearman
    maven { url="https://oss.sonatype.org/content/repositories/snapshots" }

    // Jadx
    google()
}

dependencies {
    testImplementation "junit:junit:4.13.1"

    implementation ':JadxPlus'
    implementation ':JavaGenerator'
    implementation ':jOptions'
    implementation ':LogbackPlus'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'
    implementation ':JsEngine'

    implementation ':KadabraAPI'
    implementation ':KadabraLaraApi'

    implementation ':LanguageSpecification'
    implementation ':LaraUtils'
    implementation ':LARAI'
    implementation ':WeaverInterface'
    implementation ':JavaWeaverSpecs'

    implementation "org.apache.commons:commons-lang3:3.6"
    implementation "org.apache.commons:commons-exec:1.3"
    implementation "fr.inria.gforge.spoon:spoon-core:10.1.1"
    implementation "org.json:json:20190722"
    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'org.ow2.asm', name: 'asm', version: '7.1'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'api'
        }
        resources {
            srcDir 'api'
            srcDir 'resources'
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'src-lara'
            srcDir 'resources'
        }
    }
}

application {
    mainClass.set("weaver.gui.KadabraLauncher")
}

// Kadabra
task kadabra(type: JavaExec) {
    group = "Execution"
    description = "Launches Kadabra"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    args = []
}

// Weaver Generator
task generateWeaver(type: Exec) {
    group = "Execution"
    description = "Generates the join point classes from the Language Specification"

    executable "./../../lara-framework/WeaverGenerator/build/install/WeaverGenerator/bin/WeaverGenerator"

    args = [
        '-w', 'JavaWeaver',
        '-x', './src/specs/kadabra',
        '-o', './src',
        '-p', 'weaver.kadabra',
        '-n', 'spoon.reflect.declaration.CtElement',
        '-j',
        '-d'
    ]
}